# Maintainer: Tasos Latsas <tlatsas@gmx.com>
pkgname=chruby-git
_gitname=chruby
pkgver=v0.3.6.26.ge6e4073
pkgrel=1
pkgdesc="Changes the current Ruby"
arch=('any')
url="https://github.com/postmodern/chruby.git"
license=('MIT')
depends=('bash')
#optdepends=()
makedepends=('git')
provides=('chruby')
conflicts=('chruby')
install='chruby-git.install'
source=('git+https://github.com/postmodern/chruby.git')
md5sums=('SKIP')

prepare() {
  # remove the tag version info from the Makefile
  # this is used when installing from tarballs and we
  # don't need it because it sets the docs folder to
  # chruby-version
  cd $_gitname
  sed -i 's|-$(VERSION)||g' Makefile
}

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

package() {
  cd $_gitname
  make PREFIX="$pkgdir/usr/" install
  install -D LICENSE.txt $pkgdir/usr/share/licenses/$_gitname/LICENSE
}

# vim:set ts=2 sw=2 et:
